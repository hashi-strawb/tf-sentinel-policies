# Check all resources have required tags

# Based on https://github.com/hashicorp/learn-sentinel-policy-upload/blob/main/restrict-aws-instances-type-and-tag.sentinel

# Imports mock data
import "tfplan/v2" as tfplan
import "strings"

# Get all AWS resources from all modules
aws_resources = filter tfplan.resource_changes as _, rc {
	strings.has_prefix(rc.type, "aws_") and
		rc.type is not "aws_autoscaling_group" and # TODO: these do weird stuff...
		(rc.change.actions contains "create" or rc.change.actions is ["update"])
}

# Mandatory Resource Tags
mandatory_tags = [
	"Name",
	"TTL",
	"Owner",
	"Terraform",
	"Purpose",
	"Source",
]

is_taggable = func(resource) {
	if (keys(resource.change.after) contains "tags" or keys(resource.change.after) contains "tags_all") {
		return true
	}

	return false
}

check_tags = func(resource, mt) {
	# if the resource is untaggable, this rule does not apply
	if not is_taggable(resource) {
		return true
	}

	# TODO: can we define some tags to be case insensitive?

	# Check tags explicitly defined on the resource
	if (keys(resource.change.after) contains "tags" and resource.change.after.tags is not null and resource.change.after.tags is not empty) {
		if (resource.change.after.tags contains mt) {
			return true
		}
	}

	# Check tags defined on the Terraform provider
	if (keys(resource.change.after) contains "tags_all" and resource.change.after.tags_all is not null and resource.change.after.tags_all is not empty) {
		if (resource.change.after.tags_all contains mt) {
			return true
		}
	}

	# TODO: check for tags defined as key/value
	# (i.e. tag?)
	# Check aws_autoscaling_group for an example of these

	print(resource.address, "missing tag:", mt)
	return false
}

# Unlike the example from hashicorp/learn-sentinel-policy-upload
# we're doing this in a function, so that Sentinel can check everything, rather
# than fail at the first hurdle.
#
# The reason for this is that it gives a much more verbose error log in TFC as to what's missing.
#
# In practice, all these mandatory tags would be set at the Terraform Provider level...
# but I wanted it to work like this as an example, dammit!
check_all_resources = func() {
	# Assume pass until proven otherwise
	rule_pass = true

	for aws_resources as _, resource {
		for mandatory_tags as mt {
			# if any instance of check_tags fails, set rule_pass to false
			if not check_tags(resource, mt) {
				rule_pass = false
			}
		}
	}

	return rule_pass
}

# Check that all resources have required tags
main = rule {
	(check_all_resources()) else true
}
